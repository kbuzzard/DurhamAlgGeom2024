import Mathlib
set_option linter.style.header false

/-

Let `S` be an â„•-graded ring. Recall that in Lean an â„•-grading
is represented by `ğ’œ : â„• â†’ Submodule Râ‚€ S` where `Râ‚€` is some
auxiliary base ring (for example you could imagine `Râ‚€ = â„¤`).

Recall that if `f` is a homogeneous element (e.g. `f âˆˆ ğ’œ d` for
some natural `d`) then we can form the so-called "homogeneous localization"
of `S` at `f`, written `S_{(f)}` in LaTeX and
written `HomogeneousLocalization.Away ğ’œ f` in Lean,
or just `Away ğ’œ f` if we have `open`ed `HomogeneousLocalization`).
This ring is the subring of the `â„¤`-graded ring `S[1/f]` consisting
of degree `0` elements.

In this file we show two things.

1) If `f,g` are homogeneous elements of `S`, then there's a natural map
from `S_{(f)}` to `S_{(fg)}`, sending `x/f^n` to `xg^n/(fg)^n` if `x`
is homogeneous of degree `n*deg(f)`. We show this by first constructing
the canonical map `S[1/f] â†’ S[1/fg]` and then showing that it sends the
subring `S_{(f)}` to `S_{(fg)}`. Our proof is slightly complicated by
the fact that `S_{(f)}` is not actually *defined* as a subring of `S[1/f]` in Lean,
but there is an injective map between the two rings so it's OK.

2) From (1) we can deduce that there's a bilinear map `S_{(f)} Ã— S_{(g)} â†’ S_{(fg)}`
and hence a map `S_{(f)} âŠ—[â„¤] S_{(g)} â†’ S_{(fg)}`. We prove that this map
is surjective.
-/

section ring_theory_lifting
-- let A,B,C be commutative rings
variable {A B C : Type} [CommRing A] [CommRing B] [CommRing C]
-- Say we have ring homs `Ï† : A â†’ C` and `Ïˆ : B â†’ C`, with the following two
-- properties:
-- (i) im(Ï†) âŠ† im(Ïˆ)
-- (ii) Ïˆ is injective
-- Then there's a ring hom Î± : A â†’ B making the triangle commute.
-- Proof: if a âˆˆ A then Ï†(a) âˆˆ C is in the image of Ïˆ by assumption (i)
-- so lifts to B, and by (ii) the lift is unique.

variable {Ï† : A â†’+* C} {Ïˆ : B â†’+* C} (hi : Set.range Ï† âŠ† Set.range Ïˆ)
  (hii : Function.Injective Ïˆ)

/-- A "random" lift `A â†’ B` of `Ï†` along `Ïˆ`, using hypothesis (i). Just
a function, not a ring homomorphism. See `lift_of_range_sub_range_of_injective`
for the ring homomorphism. -/
private noncomputable def lift_aux : A â†’ B :=
  fun a â†¦ (hi âŸ¨a, rflâŸ©).choose -- uses the axiom of choice in Lean's type theory
-- The fact that `lift_aux` is a lift
@[simp]
lemma lift_aux_spec (a : A) : Ïˆ (lift_aux hi a) = Ï† a :=
  (hi âŸ¨a, rflâŸ©).choose_spec

include hii in
/-- The ring homomorphism lifting `Ï†` along an injective map `Ïˆ` under
the assumption that im(Ï†) âŠ† im(Ïˆ). -/
noncomputable def lift_of_range_sub_range_of_injective : A â†’+* B where
  toFun := lift_aux hi
  map_one' := by
    apply hii
    simp
  map_mul' x y := by
    apply hii
    simp only [lift_aux_spec, map_mul]
  map_zero' := by
    apply hii
    simp
  map_add' x y := by
    apply hii
    simp

end ring_theory_lifting

noncomputable section part1
-- missing API for `Localization.awayLift`
lemma Localization.awayLift_mk {R S} [CommRing R] [CommRing S]
    (f : R â†’+* S) (r : R) (a : R) (v : S) (hv : f r * v = 1) (j : â„•) :
    Localization.awayLift f r (isUnit_iff_exists_inv.mpr âŸ¨v, hvâŸ©)
      (Localization.mk a âŸ¨r ^ j, j, rflâŸ©) = f a * v ^ j := by
  rw [Localization.mk_eq_mk']
  erw [IsLocalization.lift_mk']
  rw [Units.mul_inv_eq_iff_eq_mul]
  simp [IsUnit.liftRight, mul_assoc, â† mul_pow, (mul_comm _ _).trans hv]

variable {Râ‚€ S : Type}
-- let Râ‚€ and S be commutative rings, and let
-- S be an Râ‚€-algebra
variable [CommRing Râ‚€] [CommRing S] [Algebra Râ‚€ S]
-- Say S is â¨ ğ’œáµ¢ for i âˆˆ â„• with the ğ’œáµ¢ Râ‚€-submodules
variable (ğ’œ : â„• â†’ Submodule Râ‚€ S) [GradedAlgebra ğ’œ]
-- Say d and e are naturals
variable {d e : â„•}
-- Apparently we don't need that d,e are positive here?
-- [NeZero d] [NeZero e]
-- Say f and g are homogeneous of degrees d and e
variable {f : S} (hf : f âˆˆ ğ’œ d)
variable {g : S} (hg : g âˆˆ ğ’œ e)
-- let's not have to type HomogeneousLocalization everywhere
open HomogeneousLocalization
open Submonoid (powers)
-- Let's define a map from S_(f) to S_(fg)
variable {x : S} (hx : x = f * g)

include hx

lemma lemma1' : ((algebraMap S (Localization.Away x)) f)*(Localization.mk g (by exact âŸ¨f*g, 1, by simp [hx]âŸ©)) = 1 := by
  rw [â†Algebra.smul_def, Localization.smul_mk]
  exact Localization.mk_self âŸ¨f*g, _âŸ©

lemma lemma1 : IsUnit ((algebraMap S (Localization.Away x)) f) := by
  rw [isUnit_iff_exists_inv]
  exact âŸ¨_, lemma1' hxâŸ©
/-

         what we want
S_{(f)} -----------------> S_{(fg)}
 |                             |
 | val                         | val
 |                             |
 \/    universal property      \/
S[1/f] -----------------> S[1/(fg)]
-/
-- map1 is the diagonal map; we will later on lift it to get what we want
def map1 : Away ğ’œ f â†’+* Localization.Away x :=
  (Localization.awayLift (algebraMap S _) _ (lemma1 hx)).comp
    (algebraMap (Away ğ’œ f) (Localization.Away f))

-- this could be golfed/tidied
include hg
lemma lemma2 : Set.range (map1 ğ’œ (f := f) hx) âŠ† Set.range (val (ğ’œ := ğ’œ)) := by
  rw [Set.subset_def]
  intro y
  intro hy
  rw [Set.mem_range] at hy âŠ¢
  obtain âŸ¨z, rfl âŸ© := hy
  obtain âŸ¨âŸ¨ n, âŸ¨ a, haâŸ© , âŸ¨b, hb'âŸ© , hbâŸ© , rflâŸ© := mk_surjective z
  rw [Submonoid.mem_powers_iff] at hb
  obtain âŸ¨j, rflâŸ© := hb
  use mk âŸ¨ n+j*e, âŸ¨ a*g^j, ?_âŸ© , âŸ¨ (x)^j, ?_âŸ© , j, rflâŸ©
  Â· simp [map1]
    dsimp at hb
    rw [Localization.awayLift_mk (hv:=lemma1' hx)]
    rw [â†Algebra.smul_def, Localization.mk_pow, Localization.smul_mk]
    simp_rw [hx]
    rfl
  Â· apply SetLike.mul_mem_graded ha
    convert SetLike.pow_mem_graded _ hg
  Â· rw [hx, mul_pow]
    apply SetLike.mul_mem_graded hb'
    convert SetLike.pow_mem_graded _ hg

/-- The map needed for part (1) of the theorem. -/
def map2 : Away ğ’œ f â†’+* Away ğ’œ x :=
  lift_of_range_sub_range_of_injective (Ï† := map1 ğ’œ hx)
  (Ïˆ := algebraMap (Away ğ’œ x) (Localization.Away x))
  (lemma2 ğ’œ hg hx) (val_injective _)
-- the defining property of map2
lemma map2_spec :
    (algebraMap (Away ğ’œ x) (Localization.Away x)).comp (map2 ğ’œ hg hx) = map1 ğ’œ hx := by
  ext a
  dsimp
  have foo : Set.range â‡‘(map1 ğ’œ hx) âŠ† Set.range â‡‘(algebraMap (Away ğ’œ x) (Localization.Away x)) :=
    lemma2 ğ’œ hg hx
  exact lift_aux_spec foo a
@[simp]
lemma map2_spec' (z) :
    (map2 ğ’œ hg hx z).val = map1 ğ’œ hx z := by
  have foo : Set.range â‡‘(map1 ğ’œ hx) âŠ† Set.range â‡‘(algebraMap (Away ğ’œ x) (Localization.Away x)) :=
    lemma2 ğ’œ hg hx
  exact lift_aux_spec foo z

lemma map2_spec_mk (z) :
    (map2 ğ’œ hg hx z).val = map1 ğ’œ hx z := by
  have foo : Set.range â‡‘(map1 ğ’œ hx) âŠ† Set.range â‡‘(algebraMap (Away ğ’œ x) (Localization.Away x)) :=
    lemma2 ğ’œ hg hx
  exact lift_aux_spec foo z

lemma val_map2_mk (n a i hi) : (map2 ğ’œ hg hx (mk âŸ¨n, a, âŸ¨f ^ i, hiâŸ©, âŸ¨i, rflâŸ©âŸ©)).val =
    Localization.mk (a * g ^ i) âŸ¨x ^ i, (Submonoid.mem_powers_iff _ _).mpr âŸ¨i, rflâŸ©âŸ© := by
  rw [map2_spec_mk]
  simp [map1]
  rw [Localization.awayLift_mk (hv := lemma1' hx), â† Localization.mk_one_eq_algebraMap,
    Localization.mk_pow, Localization.mk_mul]
  simp [hx]
  congr

lemma map2_fromZeroRingHom (a) :
    map2 ğ’œ hg hx (fromZeroRingHom ğ’œ _ a) = fromZeroRingHom ğ’œ _ a := by
  ext
  convert val_map2_mk ğ’œ hg hx 0 a 0 (by simpa using SetLike.GradedOne.one_mem) <;>
    simp [fromZeroRingHom]; rfl

/-

## We now prove part (2)
-/

omit hx -- no longer needed

open scoped TensorProduct

def tensormap : Away ğ’œ f âŠ—[â„¤] Away ğ’œ g â†’+* Away ğ’œ (f * g) :=
  (Algebra.TensorProduct.lift
    (RingHom.toIntAlgHom <| map2 ğ’œ hg rfl)
    (RingHom.toIntAlgHom <| map2 ğ’œ hf <| mul_comm f g)
    (by intros; apply Commute.all)).toRingHom
-- part (2)
lemma tensormap_surjective (hd:d â‰ 0) : Function.Surjective (tensormap ğ’œ hf hg) := by
  unfold Function.Surjective
  intro z
  obtain âŸ¨âŸ¨n, âŸ¨a, haâŸ©, âŸ¨b, hb'âŸ©, âŸ¨j, (rfl : _ = b)âŸ©âŸ©, rflâŸ© := mk_surjective z
  dsimp at hb'
  dsimp
  by_cases hfg:(f * g) ^ j = 0
  Â· use 0
    have := HomogeneousLocalization.subsingleton ğ’œ (x:= Submonoid.powers (f*g)) (by
      rw [â† hfg]
      use j)
    apply this.elim
  have : n = j*(d+e) := by
    apply DirectSum.degree_eq_of_mem_mem ğ’œ hb'
    convert SetLike.pow_mem_graded _ _ using 2
    infer_instance
    exact SetLike.mul_mem_graded hf hg
    exact hfg
  let x0 : NumDenSameDeg ğ’œ (.powers f) := {
    deg := j*(d*(e+1))
    num := âŸ¨a*g^(j*(d-1)), by
      convert SetLike.mul_mem_graded ha ( SetLike.pow_mem_graded _ hg) using 2
      rw [this]
      cases d
      contradiction
      simp only [add_tsub_cancel_right, smul_eq_mul]
      ringâŸ©
    den := âŸ¨f^(j*(e+1)), by
      convert SetLike.pow_mem_graded _ hf using 2
      ringâŸ©
    den_mem := âŸ¨_,rflâŸ©
  }
  let y0 : NumDenSameDeg ğ’œ (.powers g) := {
    deg := j*(d*e)
    num := âŸ¨f^(j*e),by
      convert SetLike.pow_mem_graded _ hf using 2
      ringâŸ©
    den := âŸ¨g^(j*d),by
      convert SetLike.pow_mem_graded _ hg using 2
      ringâŸ©
    den_mem := âŸ¨_,rflâŸ©
  }
  use (mk x0 âŠ—â‚œ[â„¤] mk y0)
  simp only [tensormap]
  ext
  simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, Algebra.TensorProduct.lift_tmul,
    RingHom.toIntAlgHom_coe, val_mul, map2_spec', map1, RingHom.coe_comp, Function.comp_apply,
    HomogeneousLocalization.algebraMap_apply, val_mk]
  rw [Localization.awayLift_mk (hv:=lemma1' rfl),
    Localization.awayLift_mk (hv:=lemma1' (mul_comm _ _))]
  simp only [Localization.mk_mul, â† Localization.mk_one_eq_algebraMap, Localization.mk_pow]
  rw [Localization.mk_eq_mk_iff, Localization.r_iff_exists]
  dsimp
  use 1
  simp only [OneMemClass.coe_one, one_mul]
  cases d
  contradiction
  simp only [add_tsub_cancel_right]
  ring
